package vartas.discord.bot;

grammar Credentials extends de.monticore.literals.MCCommonLiterals{
    scope CredentialsArtifact =
        "credentials" "{"
            CredentialsName*
        "}";

    interface CredentialsName = Credentials;

    IntegerCredentials implements CredentialsName =
        Credentials "=" NatLiteral unit:Name?
    ;

    StringCredentials implements CredentialsName =
        Credentials "=" StringLiteral unit:Name?
    ;

    /*=========================================================*/
    /*=== Enum                                              ===*/
    /*=========================================================*/

    enum Credentials =
        STATUS_MESSAGE_UPDATE_INTERVAL  : "StatusMessageUpdateInterval" |
        INTERACTIVE_MESSAGE_LIFETIME    : "InteractiveMessageLifetime"  |
        ACTIVITY_UPDATE_INTERVAL        : "ActivityUpdateInterval"      |
        INVITE_SUPPORT_SERVER           : "InviteSupportServer"         |
        BOT_NAME                        : "BotName"                     |
        GLOBAL_PREFIX                   : "GlobalPrefix"                |
        WIKI_LINK                       : "WikiLink"                    |
        IMAGE_WIDTH                     : "ImageWidth"                  |
        IMAGE_HEIGHT                    : "ImageHeight"                 |
        DISCORD_TOKEN                   : "DiscordToken"                |
        REDDIT_ACCOUNT                  : "RedditAccount"               |
        REDDIT_ID                       : "RedditId"                    |
        REDDIT_SECRET                   : "RedditSecret"
    ;

    /*=========================================================*/
    /*=== AST Rules                                         ===*/
    /*=========================================================*/

    astrule IntegerCredentials =
        method public vartas.discord.bot.entities.Credentials.IntegerType getType(){
            String name = getCredentials().name();
            return vartas.discord.bot.entities.Credentials.IntegerType.valueOf(name);
        }
    ;

    astrule StringCredentials =
        method public vartas.discord.bot.entities.Credentials.StringType getType(){
            String name = getCredentials().name();
            return vartas.discord.bot.entities.Credentials.StringType.valueOf(name);
        }
    ;
}
