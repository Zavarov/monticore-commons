package vartas.discord.bot;

grammar Guild extends de.monticore.literals.MCCommonLiterals{
    symbol scope GuildArtifact =
        "guild" id:BasicLongLiteral "{"
            Entry*
        "}";

    interface Entry =
        StringLiteral
    ;

    LongGroup =
        "{"
            LongGroupElement+
        "}"
    ;

    symbol LongGroupElement =
        (type:Name ":")? element:BasicLongLiteral
    ;

    symbol BlacklistEntry implements Entry =
        "blacklist" StringLiteral
    ;

    symbol PrefixEntry implements Entry =
        "prefix" StringLiteral
    ;

    symbol scope RoleGroupEntry implements Entry =
        "rolegroup" StringLiteral LongGroup
    ;

    symbol scope SubredditGroupEntry implements Entry =
        "subreddit" StringLiteral LongGroup
    ;

    /*=========================================================*/
    /*=== AST Rules                                         ===*/
    /*=========================================================*/

    astrule Entry =
        method public String getName(){
            return getStringLiteral().getValue();
        }
    ;

    astrule LongGroupElement =
        method public String getName(){
            return getElement().getDigits();
        }
    ;

    astrule GuildArtifact =
        method public String getName(){
            return getId().getDigits();
        }
    ;

    /*=========================================================*/
    /*=== Symbol Rules                                      ===*/
    /*=========================================================*/

    symbolrule GuildArtifact =
        reference : java.nio.file.Path
    ;

    symbolrule BlacklistEntry =
        pattern : java.util.regex.Pattern
    ;
}