package vartas.discord.bot;

grammar Configuration extends de.monticore.literals.MCCommonLiterals{
    scope ConfigurationArtifact =
        "configuration" id:BasicLongLiteral "{"
            Entry*
        "}";

    interface Entry =
        StringLiteral
    ;

    LongGroup =
        "{"
            LongGroupElement+
        "}"
    ;

    LongGroupElement =
        (type:Name ":")? element:BasicLongLiteral
    ;

    BlacklistEntry implements Entry =
        "blacklist" StringLiteral
    ;

    PrefixEntry implements Entry =
        "prefix" StringLiteral
    ;

    scope RoleGroupEntry implements Entry =
        "selfassignable" StringLiteral LongGroup
    ;

    scope SubredditGroupEntry implements Entry =
        "subreddit" StringLiteral LongGroup
    ;

    /*=========================================================*/
    /*=== AST Rules                                         ===*/
    /*=========================================================*/

    astrule BlacklistEntry =
        pattern : java.util.regex.Pattern
    ;

    astrule Entry =
        method public String getName(){
            return getStringLiteral().getValue();
        }
    ;
}